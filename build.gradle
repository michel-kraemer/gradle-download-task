plugins {
    id "com.gradle.plugin-publish" version "0.19.0"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: "gradle/integrationTest.gradle"

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

group = 'de.undercouch'
version = '5.0.0-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    shadow gradleApi()
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.2'

    // Disable logging. This avoids slf4j warning and might also improve download performance.
    // See issue 141 (https://github.com/michel-kraemer/gradle-download-task/issues/141)
    implementation 'org.slf4j:slf4j-nop:1.7.32'

    testCompile 'junit:junit:4.13.2'
    testCompile 'commons-io:commons-io:2.11.0'
    testCompile 'com.github.tomakehurst:wiremock:2.23.2'
    testCompile 'org.littleshoot:littleproxy:1.1.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// improve test output when we're using a plain console (e.g. on the CI server)
test {
    jvmArgs += ['-Xmx512M']
    if (gradle.startParameter.consoleOutput == ConsoleOutput.Plain) {
        testLogging {
            events "standard_out", "passed", "skipped", "failed"
        }
    }
}

jar {
    // include license into jar
    from 'LICENSE.txt'
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "de.undercouch.gradle.tasks.download"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

gradlePlugin {
    automatedPublishing = false
}

shadowJar {
    archiveClassifier = ""
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            artifact sourcesJar
            artifact javadocJar

            pom {
               artifactId = 'gradle-download-task'
               name = 'gradle-download-task'
               packaging = 'jar'
               description = 'Adds a download task to Gradle that displays progress information'
               url = 'https://github.com/michel-kraemer/gradle-download-task'

               scm {
                   url = 'scm:git:git://github.com/michel-kraemer/gradle-download-task.git'
                   connection = 'scm:git:git://github.com/michel-kraemer/gradle-download-task.git'
                   developerConnection = 'scm:git:git://github.com/michel-kraemer/gradle-download-task.git'
               }

               licenses {
                   license {
                       name = 'The Apache Software License, Version 2.0'
                       url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution = 'repo'
                   }
               }

               developers {
                   developer {
                       id = 'michel-kraemer'
                       name = 'Michel Kraemer'
                       email = 'michel@undercouch.de'
                       url = 'https://michelkraemer.com'
                   }
               }
           }
        }
    }
}

// sign all artifacts
signing {
    useGpgCmd()
    sign publishing.publications.shadow
}

tasks.withType(Sign) {
    // only sign release artifacts and not snapshots
    onlyIf { isReleaseVersion }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

pluginBundle {
    website = 'https://github.com/michel-kraemer/gradle-download-task'
    vcsUrl = 'https://github.com/michel-kraemer/gradle-download-task'
    description = 'Adds a download task to Gradle that displays progress information'

    withDependencies {
        // remove all dependencies from the pom file just as we do it
        // when we publish the maven artifact
        it.clear()
    }

    plugins {
        greetingsPlugin {
            id = 'de.undercouch.download'
            displayName = 'gradle-download-task'
            description = 'Adds a download task to Gradle that displays progress information'
            tags = ['download', 'task', 'progress', 'url', 'server', 'file', 'http', 'https']
        }
    }

    mavenCoordinates {
        groupId = "de.undercouch"
        artifactId = "gradle-download-task"
    }
}
