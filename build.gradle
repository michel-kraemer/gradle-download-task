plugins {
    id "com.gradle.plugin-publish" version "0.20.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: "gradle/integrationTest.gradle"

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

group = 'de.undercouch'
version = '5.0.1'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    junitVersion = '5.8.2'

    // on CI server, limit functional tests to major versions to avoid running
    // out of open file descriptors (happens when we load the jar files of too
    // many Gradle distributions into memory)
    if ("true".equals(System.getenv("CI"))) {
        limitedVersionsToTest = [
            "5.6.4",
            "6.9.2",
            "7.3.3"
        ]
    } else {
        limitedVersionsToTest = versionsToTest
    }
}

repositories {
    mavenCentral()
}

configurations {
    jacocoRuntime
}

dependencies {
    shadow gradleApi()
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'

    // Disable logging. This avoids slf4j warning and might also improve download performance.
    // See issue 141 (https://github.com/michel-kraemer/gradle-download-task/issues/141)
    implementation 'org.slf4j:slf4j-nop:1.7.35'

    testImplementation "commons-io:commons-io:2.11.0"
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.32.0"
    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.mockito:mockito-core:4.3.1"
    testImplementation "xyz.rogfam:littleproxy:2.0.7"

    jacocoRuntime "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
}

java {
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// configure functional tests
task functionalTest

for (v in limitedVersionsToTest) {
    def vn = v.replace(".", "_")
    task "functionalTest_$vn"(type: Test) {
        include "**/FunctionalDownloadTest.class"
        systemProperty 'gradleVersionUnderTest', v
    }
    functionalTest.dependsOn("functionalTest_$vn")
}

test.finalizedBy(functionalTest)

tasks.withType(Test).all {
    // use junit5 for tests
    useJUnitPlatform()

    // improve test output on plain console (e.g. on CI server)
    if (gradle.startParameter.consoleOutput == ConsoleOutput.Plain) {
        testLogging {
            events "standard_out", "passed", "skipped", "failed"
        }
    }

    systemProperty 'jacocoRuntimePath', configurations.jacocoRuntime.asPath
    systemProperty 'jacocoDestFile', jacoco.destinationFile
}

jar {
    // include license into jar
    from 'LICENSE.txt'
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "de.undercouch.gradle.tasks.download"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

gradlePlugin {
    automatedPublishing = false
}

shadowJar {
    archiveClassifier = ""
}

// do not include Gradle API into shadow JAR
// see also https://github.com/johnrengelman/shadow/issues/747
configurations.named(JavaPlugin.API_CONFIGURATION_NAME) {
    dependencies.remove(project.dependencies.gradleApi())
}

// always execute shadowJar task before publishing the plugin
tasks.named("publishPlugins") {
    dependsOn shadowJar
}

// Disabling default jar task as it is overridden by shadowJar
// This also prevents us from accidentally publishing the wrong jar
tasks.named("jar").configure {
    enabled = false
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            artifact sourcesJar
            artifact javadocJar

            pom {
               artifactId = 'gradle-download-task'
               name = 'gradle-download-task'
               packaging = 'jar'
               description = 'Adds a download task to Gradle that displays progress information'
               url = 'https://github.com/michel-kraemer/gradle-download-task'

               scm {
                   url = 'scm:git:git://github.com/michel-kraemer/gradle-download-task.git'
                   connection = 'scm:git:git://github.com/michel-kraemer/gradle-download-task.git'
                   developerConnection = 'scm:git:git://github.com/michel-kraemer/gradle-download-task.git'
               }

               licenses {
                   license {
                       name = 'The Apache Software License, Version 2.0'
                       url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution = 'repo'
                   }
               }

               developers {
                   developer {
                       id = 'michel-kraemer'
                       name = 'Michel Kraemer'
                       email = 'michel@undercouch.de'
                       url = 'https://michelkraemer.com'
                   }
               }
           }
        }
    }
}

// sign all artifacts
signing {
    useGpgCmd()
    sign publishing.publications.shadow
}

tasks.withType(Sign) {
    // only sign release artifacts and not snapshots
    onlyIf { isReleaseVersion }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

pluginBundle {
    website = 'https://github.com/michel-kraemer/gradle-download-task'
    vcsUrl = 'https://github.com/michel-kraemer/gradle-download-task'
    description = 'Adds a download task to Gradle that displays progress information'

    withDependencies {
        // remove all dependencies from the pom file just as we do it
        // when we publish the maven artifact
        it.clear()
    }

    plugins {
        greetingsPlugin {
            id = 'de.undercouch.download'
            displayName = 'gradle-download-task'
            description = 'Adds a download task to Gradle that displays progress information'
            tags = ['download', 'task', 'progress', 'url', 'server', 'file', 'http', 'https']
        }
    }

    mavenCoordinates {
        groupId = "de.undercouch"
        artifactId = "gradle-download-task"
    }
}
